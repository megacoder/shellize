#!/bin/awk -f

BEGIN	{
}

{
	nStrings = 0
	delete strings
	# Hide single-quote strings
	while( match( $0, /'([^']|\\')*'/) > 0 )	{
		left   = substr($0,0,RSTART-1)
		middle = substr($0,RSTART,RLENGTH)
		# printf( "Hiding single [%s].\n", middle )
		strings[ ++nStrings ] = middle
		middle = sprintf( "\1%d;", nStrings )
		right  = substr($0,RSTART+RLENGTH)
		# printf( "%s|%s|%s\n", left, middle, right )
		$0 = left middle right
	}
	# Hide double-quote strings
	while( match( $0, /\"([^\"]|\\\")*\"/) > 0 )	{
		left   = substr($0,0,RSTART-1)
		middle = substr($0,RSTART,RLENGTH)
		# printf( "Hiding double [%s].\n", middle )
		strings[ ++nStrings ] = middle
		middle = sprintf( "\1%d;", nStrings )
		right  = substr($0,RSTART+RLENGTH)
		# printf( "%s|%s|%s\n", left, middle, right )
		$0 = left middle right
	}
	# Enclose $blah into ${blah}
	while( match( $0, /\$[A-Za-z0-9_][A-Za-z0-9_]*/ ) > 0 )	{
		left   = substr($0,0,RSTART-1)
		middle = "${" substr($0,RSTART+1,RLENGTH-1) "}"
		right  = substr($0,RSTART+RLENGTH)
		# printf( "%s|%s|%s\n", left, middle, right )
		$0 = left middle right
	}
	# Surround ${blah} with double quotes
	while( match( $0, /[^"]\$[{][^}"]*[}]/ ) > 0 )	{
		left   = substr($0,0,RSTART)
		middle = "\"" substr($0,RSTART+1,RLENGTH-1) "\""
		right  = substr($0,RSTART+RLENGTH)
		$0 = left middle right
	}
	# Surround $(blah) with double quotes
	while( match( $0, /[^"]\$[(][^)]"*[)]/ ) > 0 )	{
		left   = substr($0,0,RSTART)
		middle = "\"" substr($0,RSTART+1,RLENGTH-1) "\""
		right  = substr($0,RSTART+RLENGTH)
		$0 = left middle right
	}
	# Restore strings
	while( match( $0, /\1[0-9][0-9]*;/ ) > 0 )	{
		left   = substr($0,0,RSTART-1)
		key    = substr($0,RSTART,RLENGTH)
		middle = strings[ substr(key,2) + 0 ]
		right  = substr($0,RSTART+RLENGTH)
		$0 = left middle right
	}
	# Done with this line
	print $0
}
